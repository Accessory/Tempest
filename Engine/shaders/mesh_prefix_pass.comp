#version 450

#extension GL_EXT_control_flow_attributes:enable

struct VkDrawIndexedIndirectCommand
{
    uint indexCount;
    uint instanceCount;
    uint firstIndex;    // prefix sum
    int  vertexOffset;  // can be abused to offset into var_buffer
    uint firstInstance; // caps: should be zero

    uint self;
    uint vboOffset;
    uint padd1;
};

layout(binding = 0, std430) buffer EngineInternal0
{
    VkDrawIndexedIndirectCommand cmd[];
} indirect;

layout(binding = 1, std430) buffer EngineInternal1
{
    uint varGrow;
    uint grow;
    uint dispatchX;
    uint dispatchY;
    uint dispatchZ;
    uint desc[];
} mesh;

layout(binding = 2, std430) buffer EngineInternal2
{
    uint heap[];
} var;

layout(local_size_x = 256) in;

shared uint maxInd;
shared uint partialSummIbo[gl_WorkGroupSize.x];
shared uint partialSummVbo[gl_WorkGroupSize.x];

layout(push_constant, std140) uniform UboPush {
  uint indirectCmdCount;
  };

void main() {
  uint index = gl_LocalInvocationID.x;
  uint len   = indirectCmdCount;

  uint b = ((index+0)*len)/gl_WorkGroupSize.x;
  uint e = ((index+1)*len)/gl_WorkGroupSize.x;

  uint sumVbo = 0;
  uint sumIbo = 0;
  [[loop]]
  for(uint i=b; i<e; ++i) {
    sumIbo += indirect.cmd[i].indexCount;
    sumVbo += indirect.cmd[i].instanceCount;
    }
  partialSummIbo[index] = sumIbo;
  partialSummVbo[index] = sumVbo;

  memoryBarrierShared();
  barrier();

  uint prefixIbo = 0;
  uint prefixVbo = 0;
  for(uint i=0; i<index; ++i) {
    prefixIbo += partialSummIbo[i];
    prefixVbo += partialSummVbo[i];
    }

  [[branch]]
  if(index==255) {
    maxInd         = prefixIbo + partialSummIbo[index];
    mesh.dispatchX = (mesh.grow+64-1)/64;
    mesh.dispatchY = 1;
    mesh.dispatchZ = 1;
    }

  memoryBarrierShared();
  barrier();

  [[loop]]
  for(uint i=b; i<e; ++i) {
    uint s = prefixIbo;
    prefixIbo += indirect.cmd[i].indexCount;

    uint v = prefixVbo;
    prefixVbo += indirect.cmd[i].instanceCount;

    uint inst = indirect.cmd[i].indexCount>0 ? 1 : 0;

    indirect.cmd[i].indexCount    = 0;
    indirect.cmd[i].instanceCount = inst;
    indirect.cmd[i].firstIndex    = s;
    indirect.cmd[i].vertexOffset  = 0;
    indirect.cmd[i].firstInstance = 0;
    indirect.cmd[i].vboOffset     = maxInd + v;
    }
  }
